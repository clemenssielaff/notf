#version 320 es

precision highp float;

layout (quads, fractional_even_spacing) in;

patch in PatchData {
    float ctrl1_length;
    float ctrl2_length;
    vec2 ctrl1_direction;
    vec2 ctrl2_direction;
    int type;
} patch_data;
#define glyph_min_corner (patch_data.ctrl1_direction)
#define glyph_max_corner (patch_data.ctrl2_direction)

uniform float stroke_width;
uniform mat4 projection;
uniform float aa_width;

uniform vec2 vec2_aux1;
#define base_vertex vec2_aux1
#define atlas_size  vec2_aux1

out VertexData {
    mediump vec2 position;
    mediump vec2 tex_coord;
} v_out;

// constant symbols
const float ZERO    = 0.0f;
const float HALF    = 0.5f;
const float ONE     = 1.0f;
const float TWO     = 2.0f;
const float THREE   = 3.0f;
const float SIX     = 6.0f;

const mat4 BEZIER = mat4(
     ONE,    ZERO,   ZERO,  ZERO,
    -THREE,  THREE,  ZERO,  ZERO,
     THREE, -SIX,    THREE, ZERO,
    -ONE,    THREE, -THREE, ONE);

const mat3 DERIV = mat3(
     THREE,  ZERO, ZERO,
    -SIX,    SIX,  ZERO,
     THREE, -SIX,  THREE);

 // patch types
 const int CONVEX    = 1;
 const int CONCAVE   = 2;
 const int STROKE    = 3;
 const int TEXT      = 4;
 const int JOINT     = 31;
 const int START_CAP = 32;
 const int END_CAP   = 33;

#define start (gl_in[0].gl_Position.xy)
#define end   (gl_in[1].gl_Position.xy)

void main()
{
    if(patch_data.type == CONVEX){
        // tex_coord.y is 1 within the stroke and 0 at the very edge (geometric anti-aliasing in the normal)
        v_out.tex_coord.y = aa_width == 0.0 ? 1.0 : ONE - step(0.9, gl_TessCoord.y);
        v_out.tex_coord.x = gl_TessCoord.x;

        // This always creates a triangle with zero area :/ but I hope that the GPU is quick to discard such polygons.
        // The alternative would be that I always pass 3 vertices to a patch, which would mean that I always pass an unused
        // vertex for each line segment, which might be slower
        vec2 delta = mix(gl_in[1].gl_Position.xy, gl_in[0].gl_Position.xy, gl_TessCoord.x) - base_vertex;
        v_out.position = fma(vec2(step(HALF, gl_TessCoord.y) * (length(delta) - (sign(v_out.tex_coord.y - HALF) * aa_width))),
                             normalize(delta), base_vertex);
    }

    else if (patch_data.type == CONCAVE) {
        // concave shapes have no defined outer edge that can be smoothed, therefore they have no geometric aa
        v_out.tex_coord.y = 1.0;
        v_out.tex_coord.x = gl_TessCoord.x;

        // see comment in the CONVEX branch
        vec2 delta = mix(gl_in[1].gl_Position.xy, gl_in[0].gl_Position.xy, gl_TessCoord.x) - base_vertex;
        v_out.position = fma(vec2(step(HALF, gl_TessCoord.y) * length(delta)), normalize(delta), base_vertex);
    }

    else if (patch_data.type == TEXT) {
        v_out.tex_coord.y = 1.0;
        v_out.tex_coord.x = 1.0;

        v_out.position = mix(glyph_min_corner, glyph_max_corner, gl_TessCoord.xy);
    }

    else { // patch_data.type is some form of line
        // tex_coord.y is 1 within the stroke and 0 at the very edge (geometric anti-aliasing in the normal)
        v_out.tex_coord.y = aa_width == 0.0 ? 1.0 : ONE - (step(ONE, gl_TessCoord.y) + step(ONE, ONE - gl_TessCoord.y));

        float normal_offset = sign(gl_TessCoord.y - HALF) * HALF * (max(ZERO, stroke_width) + ((ONE - (v_out.tex_coord.y * TWO)) * aa_width));

        if(patch_data.type == STROKE){
            vec2 TVEC = vec2(ONE, gl_TessCoord.x);

            // bezier control points
            vec2 ctrl1 = start + (patch_data.ctrl1_direction * patch_data.ctrl1_length);
            vec2 ctrl2 = end + (patch_data.ctrl2_direction * patch_data.ctrl2_length);

            // tex_coord.x grows along the stroke from start = 0 to end = 1
            v_out.tex_coord.x = gl_TessCoord.x;

            // screen space position of the vertex
            vec2 normal = normalize(mat3x2(ctrl1-start, ctrl2-ctrl1, end-ctrl2) * DERIV * (TVEC.xyy * TVEC.xxy)).yx * vec2(-ONE, ONE);
            v_out.position =
                    (mat4x2(start, ctrl1, ctrl2, end) * BEZIER * (TVEC.xyyy * TVEC.xxyy * TVEC.xxxy)).xy    // along spline
                    + (normal * normal_offset);                                                             // along normal
        }

        else if(patch_data.type == JOINT){
            vec2 normal;
            if(gl_TessCoord.x == ZERO){
                normal = (-patch_data.ctrl1_direction).yx * vec2(-ONE, ONE);
            } else {
                normal = (-patch_data.ctrl2_direction).yx * vec2(ONE, -ONE);
            }
            v_out.position = start + (normal * normal_offset);

            v_out.tex_coord.x = HALF;
        }

        else if(patch_data.type == START_CAP){
            // screen space position of the vertex
            vec2 normal = patch_data.ctrl1_direction.yx * vec2(-ONE, ONE);
            v_out.position = start
                    + (patch_data.ctrl1_direction * ((gl_TessCoord.x - ONE) * aa_width * TWO))  // along spline
                    + (normal * normal_offset);                                                 // along normal

            // cap texture coordinates
            v_out.tex_coord.x = ZERO;
            v_out.tex_coord.y = (gl_TessCoord.x == ONE) ? v_out.tex_coord.y : ZERO;
        }

        else if(patch_data.type == END_CAP){
            // screen space position of the vertex
            vec2 normal = patch_data.ctrl2_direction.yx * vec2(ONE, -ONE);
            v_out.position = end
                    + (patch_data.ctrl2_direction * (-gl_TessCoord.x * aa_width * TWO)) // along spline
                    + (normal * normal_offset);                                         // along normal

            // cap texture coordinates
            v_out.tex_coord.x = ONE;
            v_out.tex_coord.y = gl_TessCoord.x > 0.9 ? ZERO : v_out.tex_coord.y;
        }
    }

    // clip-space position of the vertex
    gl_Position = projection * vec4(v_out.position, -ONE, ONE);
}
